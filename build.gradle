buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'http://repo.spring.io/plugins-release'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath("org.springframework.build.gradle:propdeps-plugin:0.0.7")
    }
}

plugins {
    id "io.spring.dependency-management" version "1.0.0.RELEASE"
}

ext {
    springBootVersion = "1.5.2.RELEASE"
    junitVersion = "4.12"
    junitHierarchicalcontextrunnerVersion = "4.12.1"
    assertjVersion = "3.5.2"
}

group 'com.github.thekingnothing'
version '0.1.0'

subprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
}

project(":kalah-core") {
    description = "Game engine."

    dependencies {
        testCompile("junit:junit:${junitVersion}")
        testCompile("de.bechte.junit:junit-hierarchicalcontextrunner:${junitHierarchicalcontextrunnerVersion}")
        testCompile("org.assertj:assertj-core:${assertjVersion}")
    }
}

project(":kalah-service") {
    dependencies {
        compile(project(":kalah-core"))
        compile("org.springframework.boot:spring-boot-starter")
        testCompile("org.springframework.boot:spring-boot-starter-test")
    }

    dependencyManagement {
        imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
    }
}

project(":kalah-rest") {
    dependencies {
        compile(project(":kalah-service"))
        compile("org.springframework:spring-web")
        compile("org.springframework:spring-webmvc")
        testCompile("org.springframework.boot:spring-boot-starter-test")
    }

    dependencyManagement {
        imports { mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}") }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}

